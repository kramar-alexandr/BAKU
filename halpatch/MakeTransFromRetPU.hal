external procedure InitOTCheckArray(record OTCheckBlock,Array string,Array string,var Integer);
external function Integer CheckObjsNew(string,string,Array string,Array string,Integer,var string);
external function Integer CheckObjs(string,string,var string);
external function string 60 AddObjectToObjectList(string,string);
external function roundmode DefaultRoundMode();
external function Integer ItemPrimaryCostModel(record INVc,var Integer);
external procedure GetConsigmentStockAcc(string,string,var string);
external function Integer TRVc_AddRoundOffRow(var record TRVc,val,val,string,Boolean,Boolean,Boolean,val,var val);
external function Boolean CheckTTRPerRow(string,string,string,Boolean,var string,Integer);
external function string 20 FindItemPriceVarianceAcc(string,string,Integer);
external procedure GetITPurchAcc(string,var string,Integer);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external procedure GetITStockAcc(string,var string);
external procedure GetITCostAcc(string,var string);
external function Boolean TransInFiscal(Date);
external function Boolean GetAccName(string,var string,Integer);
external procedure AddEuroTrRowType(record TRVc,string,string,string,string,val,val,val,Boolean,Boolean,var val,Boolean,string,string,string,Integer);

procedure GetRetPUStockAcc(Integer ItemGroupAccounts,string location,string MainStock,string itgroup,string arstockacc,var string res,var string locobjstr)
BEGIN
  record LocationVc LocRec;

  res = "";
  locobjstr = "";
  LocRec.Code = location;
  if (blank(LocRec.Code)) then begin
    LocRec.Code = MainStock;
  end;
  if (ReadFirstMain(LocRec,1,true)) then begin
    if (nonblank(LocRec.StockAcc)) then begin
      res = LocRec.StockAcc;
    end;    
    locobjstr = LocRec.Objects;
  end;
  if (nonblank(res)) then begin
    goto LGetRetPUStockAcc;
  end;
  if (ItemGroupAccounts!=0) then begin
    GetITStockAcc(itgroup,res);
    if (nonblank(res)) then begin
      goto LGetRetPUStockAcc;
    end;
  end;
  res = arstockacc;
  if (nonblank(res)) then begin
    goto LGetRetPUStockAcc;
  end;
LGetRetPUStockAcc:;  
  RETURN;
END;

procedure GetRetPUPControlAcc(Integer ItemGroupAccounts,string purowacc,string itgroup,record AccBlock ARAccRec,var string purchacc,string vecode)
BEGIN
  Integer res;
  integer ctype;
  record CUVc VEp;
  
  purchacc = "";
  if (ARAccRec.UsePUAccInRetPU!=0) then begin   
    purchacc = purowacc;
    if (nonblank(purchacc)) then begin
      goto LGetRetPUPControlAcc;
    end;
    if (ItemGroupAccounts!=0) then begin
      VEp.Code = vecode;
      if ReadFirstMain(VEp,1,true) then begin
        if (VEp.ExportFlag==0) then begin
          ctype = 0;
        end;
        if ((VEp.ExportFlag==1) or (VEp.ExportFlag==3)) then begin
          ctype = 1;
        end;
        if (VEp.ExportFlag==2) or (VEp.ExportFlag==4) then begin
          ctype = 2;
        end;  
      end;
      GetITPurchAcc(itgroup,purchacc,ctype);
      if (nonblank(purchacc)) then begin
        goto LGetRetPUPControlAcc;
      end;
    end;
    purchacc = ARAccRec.PurchAcc;
    if (nonblank(purchacc)) then begin
      goto LGetRetPUPControlAcc;
    end;
  end else begin
    purchacc = ARAccRec.RetPUAcc;
  end;
LGetRetPUPControlAcc:;  
  RETURN;
END;

procedure GetRetPUCostAcc(Integer ItemGroupAccounts,string retpurowcostacc,string incostacc,string itgroup,var string res)
BEGIN
  res = "";
  res = retpurowcostacc;
  if (nonblank(res)) then begin
    goto LGetRetPUCostAcc;
  end;
  res = incostacc;
  if (nonblank(res)) then begin
    goto LGetRetPUCostAcc;
  end;
  if (ItemGroupAccounts!=0) then begin
    GetITCostAcc(itgroup,res);
    if (nonblank(res)) then begin
      goto LGetRetPUCostAcc;
    end;
  end;
LGetRetPUCostAcc:;
  RETURN;
END;

function Integer AddRetPUCostRow(record TRVc gTRp,record MainStockBlock MainStockRec,record CostAccBlock CostAccRec,record AccBlock ARAccRec,
                                 record APAccBlock APAb,record INVc INr,
                                 val baserate1,val baserate2,
                                 record RetPUVc RetPUp,row RetPUVc RetPUrw,Boolean dc1,Boolean dc2,Boolean sumupf,var val TotSum)
BEGIN
  Integer res,primarycostmodel,queuecostmodel;
  val t1,rs,fifo;
  string 255 objstr,tstr,locobjstr,errstr;
  string 10 retpucostacc,stockacc,varianceacc,taccnumber,location;
  Array string 255 otcheckaccs;
  Array string 255 otcheckobjtyps;
  Integer otcheckcnt;
  record OTCheckBlock OTCheckr;
  Boolean initotcheckf;  
  
  t1 = RetPUrw.Quant*RetPUrw.PUCostPrice;
  fifo = RetPUrw.FIFORowVal;
  fifo = Round(fifo,DefaultRoundMode);
  rs = rs + t1;
  tstr = INr.Name;
  objstr = RetPUrw.Objects; 
  if (APAb.SkipObjectsOnVIFromHeader==0) then begin
    objstr = AddObjectToObjectList(objstr,RetPUp.Objects);
  end;
  GetRetPUPControlAcc(CostAccRec.ItemGroupAccounts,RetPUrw.CredAcc,INr.Group,ARAccRec,retpucostacc,RetPUp.VECode);
//  GetRetPUCostAcc(CostAccRec.ItemGroupAccounts,RetPUrw.CostAcc,INr.CostAcc,INr.Group,retpucostacc);
  if (GetAccName(retpucostacc,tstr,60)==false) then begin
    res = 20596;
    goto LAddRetPUCostRow;
  end;
  if (ARAccRec.BookPriceVar!=0) then begin
    if (RetPUrw.VarianceAmount!=0) then begin
      t1 = t1 + RetPUrw.VarianceAmount;
    end;
  end;

  primarycostmodel = ItemPrimaryCostModel(INr,queuecostmodel);
  if (CostAccRec.BookInPriceVariance!=0) and (primarycostmodel==1) and (queuecostmodel!=20) then begin
    if (CheckTTRPerRow("RetPUVc",retpucostacc,objstr,dc1,taccnumber,1)) then begin end;      
    AddEuroTrRowType(gTRp,retpucostacc,objstr,"",tstr,RetPUrw.Quant*RetPUrw.PUCostPrice,baserate1,baserate2,sumupf,dc1,TotSum,true,taccnumber,"","",kTransactionRowTypeStock);
    if (GetAccName(ARAccRec.InPriceVarAcc,tstr,60)==false) then begin
      res = 22048;
      goto LAddRetPUCostRow;
    end;        
    if (CheckTTRPerRow("RetPUVc",ARAccRec.InPriceVarAcc,objstr,dc1,taccnumber,1)) then begin end;      
    AddEuroTrRowType(gTRp,ARAccRec.InPriceVarAcc,objstr,"",tstr,t1-RetPUrw.Quant*RetPUrw.PUCostPrice,baserate1,baserate2,sumupf,dc1,TotSum,true,taccnumber,"","",kTransactionRowTypeStock);
  end else begin
    if (CheckTTRPerRow("RetPUVc",retpucostacc,objstr,dc1,taccnumber,1)) then begin end;      
    AddEuroTrRowType(gTRp,retpucostacc,objstr,"",tstr,t1,baserate1,baserate2,sumupf,dc1,TotSum,true,taccnumber,"","",kTransactionRowTypeStock);
 end;
  objstr = RetPUrw.Objects;
  if ((APAb.SkipObjectsOnVIFromHeader==0) and (APAb.VEObjonPurchAcc!=0)) then begin
    objstr = AddObjectToObjectList(objstr,RetPUp.Objects);
  end;      
  if (ARAccRec.BookPriceVar!=0) then begin
    if (RetPUrw.VarianceAmount!=0) then begin              
      varianceacc = FindItemPriceVarianceAcc(ARAccRec.ItemPriceVar,INr.Group,CostAccRec.ItemGroupAccounts);
      if (GetAccName(varianceacc,tstr,60)==false) then begin
        res = 2139;      
        goto LAddRetPUCostRow;
      end;
      if (CheckTTRPerRow("RetPUVc",varianceacc,objstr,dc2,taccnumber,1)) then begin end;      
      AddEuroTrRowType(gTRp,varianceacc,objstr,"",tstr,RetPUrw.VarianceAmount,baserate1,baserate2,true,dc2,TotSum,true,taccnumber,"","",kTransactionRowTypeStock);
    end;
  end;      
  if ((t1-fifo)!=0) then begin
    if (GetAccName(ARAccRec.RetPUVar,tstr,60)==false) then begin
      res = 22042;
      goto LAddRetPUCostRow;
    end;
    if (CheckTTRPerRow("RetPUVc",ARAccRec.RetPUVar,objstr,dc2,taccnumber,1)) then begin end;      
    AddEuroTrRowType(gTRp,ARAccRec.RetPUVar,objstr,"",tstr,t1-fifo,baserate1,baserate2,sumupf,dc2,TotSum,true,taccnumber,"","",kTransactionRowTypeStock);
  end;
  location = RetPUrw.Location;
  if (blank(location)) then begin
    location = RetPUp.Location;
  end;  
  GetRetPUStockAcc(CostAccRec.ItemGroupAccounts,location,MainStockRec.MainStock,INr.Group,ARAccRec.StockAcc,stockacc,locobjstr);
  objstr = AddObjectToObjectList(objstr,locobjstr);
  GetConsigmentStockAcc(RetPUrw.ArtCode,RetPUrw.SerialNr,stockacc);
  if (GetAccName(stockacc,tstr,60)==false) then begin
    res = 2120;
    goto LAddRetPUCostRow;
  end;
  if (initotcheckf==false) then begin
    InitOTCheckArray(OTCheckr,otcheckaccs,otcheckobjtyps,otcheckcnt);
    initotcheckf = true;
  end;
  res = CheckObjsNew(stockacc,objstr,otcheckaccs,otcheckobjtyps,otcheckcnt,errstr);      
  if (res!=0) then begin
    goto LAddRetPUCostRow;
  end;
  if (CheckTTRPerRow("RetPUVc",stockacc,objstr,dc2,taccnumber,1)) then begin end;      
  AddEuroTrRowType(gTRp,stockacc,objstr,"",tstr,fifo,baserate1,baserate2,sumupf,dc2,TotSum,true,taccnumber,"","",kTransactionRowTypeStock);
LAddRetPUCostRow:;      
  AddRetPUCostRow = res;      
  RETURN;
END;

global
function Integer MakeTransFromRetPU(record TRVc gTRp,record RetPUVc RetPUp,Boolean rpt)
BEGIN
  Integer res;
  record MainStockBlock MainStockRec;
  record CostAccBlock CostAccRec;
  record TRGenBlock TRGenRec;
  record AccBlock ARAccRec;
  record LocationVc LocRec;
  record INVc INr;
  record ITVc ITr;
  record SHVc recSHr;
  record TRVc locTRr;
  row RetPUVc RetPUrw;
  row SHVc SH2rw;
  Integer rwcnt,rwcnt2;
  Integer i,j,error;
  string 255 ftxt,tstr;
  string 255 firstVAT;
  val t1,VATSum,TotSum;
  val t,rs,structrs;
  val baserate1,baserate2,temp;
  Boolean dc1,dc2;
  Boolean isstruct,infound,sumupf;
  string 10 shstockacc,itshstockacc,retpucostacc,stockacc;
  string 5 orobjstr;
  string 255 objstr;
  string 10 location;
  string 20 curncy;
  record APAccBlock APAb;
  transaction string 255 gRuniningMaint;
  row TRVc TRrw;// Edit ************************** Tuesday, 10 February 2015 16:03:57
          
  BlockLoad(MainStockRec);
  BlockLoad(CostAccRec);
  BlockLoad(ARAccRec);
  BlockLoad(TRGenRec);
  BlockLoad(APAb);
  sumupf = true;
  if (CostAccRec.CorespondingAccounts!=0) then begin
    sumupf = false;
  end;
  RecordNew(gTRp);
  if (rpt==false) then begin
    if (TRGenRec.RetPUGenTrans==0) then begin
      res = 0;
      goto LMakeTransFromRetPU;
    end;
  end;  
  if (rpt==false) then begin
    if ((nonblankdate(TRGenRec.RetPUStartDate)) and
        (RetPUp.TransDate<TRGenRec.RetPUStartDate)) then begin
      res = 0;
      goto LMakeTransFromRetPU;
    end;
  end;
  if (true) then begin
    gTRp.IntYc = RetPUYc;
    gTRp.Number = RetPUp.SerNr;
    gTRp.RefStr = "";
  end;
  if (rpt==false) then begin
    locTRr.IntYc = gTRp.IntYc;
    locTRr.Number = gTRp.Number;
    if (ReadFirstMain(locTRr,0,true)) then begin
      switch (gRuniningMaint) begin    
        case "RecalcStockMn":  ;
        otherwise
          res = 1032;
          goto LMakeTransFromRetPU;
      end;
    end; 
  end;
  objstr = "";
  orobjstr = "";  
  gTRp.RegDate = CurrentDate;
  gTRp.RegTime = CurrentTime;
  gTRp.TransDate = RetPUp.TransDate;
  if (TransInFiscal(gTRp.TransDate)==false) then begin
    res = 1075;
    goto LMakeTransFromRetPU;
  end;
  curncy = "";
  curncy = RetPUp.CurncyCode;
  GetFullCurncyRate(curncy,gTRp.TransDate,temp,temp,temp,baserate1,baserate2);
  ftxt = RetPUp.Comment;
  gTRp.Comment = ftxt;
  TotSum = 0;
  VATSum = 0;
  firstVAT = "";
  dc1 = true;
  dc2 = false;
  rwcnt = MatRowCnt(RetPUp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(RetPUp,i,RetPUrw);
    retpucostacc = "";
    if (nonblank(RetPUrw.CostAcc)) then begin
      retpucostacc = RetPUrw.CostAcc;
    end;
    INr.Code = RetPUrw.ArtCode;
    if (ReadFirstItem(RetPUrw.ArtCode,INr,true,true)==false) then begin
      INr.Objects = "";
      tstr = "";
    end;  
    error = AddRetPUCostRow(gTRp,MainStockRec,CostAccRec,ARAccRec,APAb,INr,baserate1,baserate2,RetPUp,RetPUrw,dc1,dc2,sumupf,TotSum);
    if (error!=0) then begin
      res = error;
      goto LMakeTransFromRetPU;
    end;
  end;
  rwcnt = matrowcnt(gTRp);
  for (i=0;i<rwcnt;i=i+1) begin
  	matrowget(gTRp,i,TRrw);
  		TRrw.Curncy = RetPUp.CurncyCode;
      TRrw.FrRate = RetPUp.FrRate;
      TRrw.ToRateB1 = RetPUp.ToRateB1;
      TRrw.ToRateB2 = RetPUp.ToRateB2;
      TRrw.BaseRate1 = RetPUp.BaseRate1;
      TRrw.BaseRate2 = RetPUp.BaseRate2;
      
      if(TRrw.DebVal!=0)then begin
				TRrw.CurDebVal = round((TRrw.DebVal * TRrw.FrRate / TRrw.ToRateB1),defaultcurroundoff);// Edit ************************** Tuesday, 3 February 2015 16:13:21
			end;
			if(TRrw.CredVal!=0)then begin
				TRrw.CurCredVal = round((TRrw.CredVal * TRrw.FrRate / TRrw.ToRateB1),defaultcurroundoff);// Edit ************************** Tuesday, 3 February 2015 16:13:21
      end;
  	matrowput(gTRp,i,TRrw);
  end;
  
  
  res = TRVc_AddRoundOffRow(gTRp,baserate1,baserate2,"",false,false,false,TotSum,TotSum);
  if (res!=0) then begin
    goto LMakeTransFromRetPU;
  end;
LMakeTransFromRetPU:;
  MakeTransFromRetPU = res;
  RETURN;
END;
